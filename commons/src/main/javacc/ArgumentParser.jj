options {
    JDK_VERSION = "1.7";
    IGNORE_CASE = true;
    STATIC = false;
    DEBUG_TOKEN_MANAGER = false;
    UNICODE_INPUT = true;
}

PARSER_BEGIN(ArgumentParser)
/*
 * Copyright (c) 2011 - 2012. Elega9t Ltd. All rights reserved.
 * ELEGA9T PROPRIETARY/CONFIDENTIAL. Use is subject to license terms.
 */

package com.elega9t.commons.args;

import java.util.Map;
import java.util.List;
import java.util.Arrays;
import java.util.Collections;
import java.util.LinkedHashMap;

import com.elega9t.commons.args.Argument;
import com.elega9t.commons.args.NamedArgument;
import com.elega9t.commons.args.DuplicateArgumentException;

public class ArgumentParser {
}

PARSER_END(ArgumentParser)

<*> SKIP: {
  " " | "\t"
  {
  System.err.println("Skipping");
  }
}

<*> TOKEN : {
  <VALUE_STRING_NB : ( ~["-", " ", "\t"] ( ~["\r", "\n", " ", "\t"] )* ) >
| <SINGLE_QUOTED_STRING_NB : ( "'" ( ~["'", "\r", "\n"] )* "'" ) >
| <DOUBLE_QUOTED_STRING_NB : ( "\"" ( ~["\"", "\r", "\n"] )* "\"" ) >
| <WHITESPACE       : ( " " | "\t" ) >
| <OPTION_PREFIX    : ( "-" ) >
}

Map<String, Argument> parse(List<String> booleanParameters) :
{
    Map<String, Argument> arguments = new LinkedHashMap<String, Argument>();
    Argument argument;
    int unnamedParameterCount = 0;
}
{
    ( argument = nextParameter(booleanParameters)
        {
            if(argument instanceof NamedArgument) {
                if (arguments.containsKey(((NamedArgument)argument).getName())) {
                    throw new DuplicateArgumentException(((NamedArgument)argument).getName());
                } else {
                    arguments.put(((NamedArgument)argument).getName(), argument);
                }
            } else {
                arguments.put(unnamedParameterCount+"", argument);
                unnamedParameterCount++;
            }
        }
    )* <EOF>
    {
        return arguments;
    }
}

Argument nextParameter(List<String> booleanParameters) :
{
    Argument argument;
    String name;
}
{
    ((
        <OPTION_PREFIX><VALUE_STRING_NB>
        {
            name = token.toString();
            argument = new NamedArgument(name);
            if(!booleanParameters.contains(name))
                argument.setValue(getValue());
            else
                argument.setValue("true");
        }
    )
    |
    (
        ( <VALUE_STRING_NB> | <SINGLE_QUOTED_STRING_NB> | <DOUBLE_QUOTED_STRING_NB> )
        {
            argument = new Argument();
            argument.setValue(token.toString());
        }
    ))
    {
            return argument;
    }
}

String getValue() :
{
}
{
        ( <VALUE_STRING_NB> | <SINGLE_QUOTED_STRING_NB> | <DOUBLE_QUOTED_STRING_NB> ) { return token.toString(); }
}